apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'

allprojects {
    apply plugin: 'idea'

    group = 'br.com.ead'

    apply from: rootProject.file("gradle/dependency-management.gradle")

    tasks.withType(JavaCompile) {
        options.incremental = true
        options.encoding = 'UTF-8'
    }

    plugins.withType(ApplicationPlugin) {
        distZip.enabled = false
    }

}

buildscript {
    ext {
        springBootVersion = "2.6.2"
        dockerComposeVersion = "0.14.9"
    }

    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "gradle.plugin.com.avast.gradle:gradle-docker-compose-plugin:${dockerComposeVersion}"
    }
}

apply plugin: 'com.avast.gradle.docker-compose'

dockerCompose.isRequiredBy(test)

dockerCompose {
    projectName = project.name
    projectNamePrefix = 'docker-compose'
    stopContainers = true
    removeContainers = true
    removeOrphans = true
}

test {
    useJUnitPlatform()
    testLogging {
        events "skipped", "failed"
    }
}
